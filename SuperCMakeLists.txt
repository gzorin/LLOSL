# --*-- CMAKE --*--

include(ExternalProject)

# Boost
ExternalProject_Add(boost
  SOURCE_DIR ${PROJECT_SOURCE_DIR}/extsrc/boost
  CONFIGURE_COMMAND ${PROJECT_SOURCE_DIR}/extsrc/boost/bootstrap.sh
  BUILD_COMMAND ${PROJECT_SOURCE_DIR}/extsrc/boost/b2 -d0
  --build-dir=${PROJECT_BINARY_DIR}/extsrc/boost --prefix=${LLOSL_DEPS_DIR}
  --with-system --with-filesystem --with-regex --with-thread --with-wave --with-date_time
  --no-cmake-config
  variant=release link=static cxxstd=14 install
  BUILD_IN_SOURCE 1
  INSTALL_COMMAND ""
)

# OpenImageIO
ExternalProject_Add(oiio
  SOURCE_DIR "${PROJECT_SOURCE_DIR}/extsrc/oiio"
  BINARY_DIR "${PROJECT_BINARY_DIR}/extsrc/oiio"
  DEPENDS boost
  LIST_SEPARATOR ^^
  CMAKE_ARGS
  -DCMAKE_INSTALL_PREFIX=${LLOSL_DEPS_DIR}
  -DBUILDSTATIC=ON -DOIIO_BUILD_TESTS=OFF
  -DBOOST_INCLUDEDIR:PATH=${LLOSL_DEPS_DIR}/include -DBOOST_LIBRARYDIR:PATH=${LLOSL_DEPS_DIR}/lib -DBoost_NO_SYSTEM_PATHS:BOOL=ON)

# OSL
ExternalProject_Add(osl
  SOURCE_DIR "${PROJECT_SOURCE_DIR}/extsrc/OpenShadingLanguage"
  BINARY_DIR "${CMAKE_BINARY_DIR}/extsrc/OpenShadingLanguage"
  DEPENDS oiio boost
  LIST_SEPARATOR ^^
  CMAKE_ARGS
  -DCMAKE_INSTALL_PREFIX=${LLOSL_DEPS_DIR}
  -DCMAKE_MODULE_PATH:LIST=${CMAKE_MODULE_PATH_FOR_EXTERNAL}
  -DLLVM_DIR=${LLVM_DIR} -DLLVM_STATIC=ON
  -DBUILDSTATIC=ON -DOSL_BUILD_TESTS=OFF -DOSL_BUILD_PROGRAMS=OFF -DOSL_BUILD_PLUGINS=OFF
  -DOSL_NO_DEFAULT_TEXTURESYSTEM=OFF -DUSE_BOOST_WAVE=ON -DUSE_LLVM_BITCODE=OFF -DENABLERTTI=TRUE
  -DUSE_PARTIO=OFF
  -DBOOST_INCLUDEDIR:PATH=${LLOSL_DEPS_DIR}/include -DBOOST_LIBRARYDIR:PATH=${LLOSL_DEPS_DIR}/lib -DBoost_NO_SYSTEM_PATHS:BOOL=ON)

# LLOSL
ExternalProject_Add(llosl
  SOURCE_DIR "${PROJECT_SOURCE_DIR}"
  BINARY_DIR "${PROJECT_BINARY_DIR}"
  DEPENDS oiio boost osl
  INSTALL_COMMAND ""
  CMAKE_ARGS
  -DLLOSL_SUPERBUILD:BOOL=OFF)
